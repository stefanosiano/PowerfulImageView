apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//fixed data
def pkgName = 'powerfulimageview'
def libGroupId = "com.stefanosiano"                                          // Maven Group ID for the artifact
def libArtifactId = "powerfulimageview"                                      // Maven Artifact ID for the artifact
def libName = "Powerful Image View"                                          // Library name
def siteUrl = 'https://github.com/stefanosiano/PowerfulImageView'            // Homepage URL of the library
def gitUrl = 'https://github.com/stefanosiano/PowerfulImageView.git'         // Git repository URL
def libDescription = 'Custom Android ImageView with several added features.' // Library description

//data to change on updates
def libVersion = "0.3.3"// This is the library version used when deploying the artifact
def gitTag = '0.3.3'

group = libGroupId //bintray org/group name
version = libVersion //version


android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName libVersion
        consumerProguardFiles 'piv-proguard-rules.txt'
        vectorDrawables.useSupportLibrary = true

        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true
        buildToolsVersion "26.0.0"

    }
}
dependencies {

    compile 'com.android.support:appcompat-v7:26.0.0'
}






install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId libGroupId
                artifactId libArtifactId

                // Add your description here
                name libName
                description libDescription
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id 'stefanosiano'
                        name 'Stefano Siano'
                        email 'stefano.siano91@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}


task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task generateJavaDocsJar(type: Jar) {
    from generateJavadocs.destinationDir
    classifier 'javadoc'
}

generateJavaDocsJar.dependsOn generateJavadocs

artifacts {
    archives generateJavaDocsJar
    archives generateSourcesJar
}




// https://github.com/bintray/gradle-bintray-plugin
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    // Package info for BinTray
    pkg {
        repo = 'maven-repo'
        // it is the name that appears in bintray when logged
        name = pkgName
        desc = libDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
        publish = true
        publicDownloadNumbers = true
        version {
            name = libVersion
            desc = libDescription
            released = new Date()
            vcsTag = gitTag
        }
    }
}